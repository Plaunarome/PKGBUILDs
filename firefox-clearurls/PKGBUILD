# Maintainer: Robert Cegli≈Ñski <rob.ceglinski@gmail.com>
# Maintainer: Plaunarome <plaunarome@protonmail.com>

_name="ClearUrls"
_uuid="{74145f27-f039-47ce-a470-a662b129930a}"
pkgname=firefox-clearurls
pkgver=1.20.0
pkgrel=2
pkgdesc="Removes tracking elements from URLs"
arch=("any")
url="https://clearurls.xyz"
license=("LGPL3")
groups=("firefox-addons")
makedepends=("libarchive")
source=("https://gitlab.com/KevinRoebert/${_name}/-/archive/${pkgver}/${_name}-${pkgver}.tar.gz"
	"fix_corrupt_package.patch")
sha512sums=('a1d1a474ca634d1a8f2977477e7ef498b839563d952e8f2077f7a12bef89ebc36ed57194e98ce56e0cde6a3f990c25961d2272fc5aa711aab51b0361755e6d9d'
            'da35db2fd7cfbfc6f3e9e9e05e700731bd23b459d55ae5b953e5ca5e05313c0626086afe7fb4510bcee93e4f2e2e7a517822cda98eb7221ad68e5c69342419cb')

prepare() {
	patch --directory "${_name}-${pkgver}" --strip 1 < fix_corrupt_package.patch
}

build() {
	cd "${_name}-${pkgver}"
	bsdtar -caf "../${_name}.zip" -- \
		_locales/* \
		browser-polyfill.js \
		clearurls.js \
		core_js/* \
		css/* \
		external_js/* \
		fonts/* \
		html/* \
		img/* \
		manifest.json
}

package() {
	install -D --mode 644 -- "${_name}.zip" \
		"${pkgdir}/usr/lib/firefox/browser/extensions/${_uuid}.xpi"
}
